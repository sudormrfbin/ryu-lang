%import common.INT
%import common.NEWLINE -> _NL
%import common.CNAME -> IDENTIFIER 

%ignore " "

?start: statement_block

?statement_block: statement
			   | (statement _NL)+ statement?

?statement: expression
		  | variable_declaration

variable_declaration: "let" IDENTIFIER "=" expression

?expression: equality

?!equality: logical ( "==" | "!=" ) equality
	 | logical

?!logical: comparison ( "&&" | "||" ) logical
	 | comparison

?!comparison: term ( ">" | "<" | "<=" | ">=") comparison
	 | term

?!term: factor ("+" | "-" ) term
	 | factor

?!factor: unary_op
	    | unary_op ("*" | "/" | "%") factor

?!unary_op: ("+" | "-" | "!") unary_op
		  | atom

?atom: bool_literal
	 | int_literal
	 | string_literal
	 | "(" start ")"
	 | variable

bool_literal: TRUE | FALSE
TRUE: "true"
FALSE: "false"

int_literal: INT

string_literal: STRING
STRING: "\"" /.*?/ "\""

variable: IDENTIFIER
