%import common.INT
%import common.NEWLINE -> _NL
%import common.CNAME -> IDENTIFIER 

%ignore " "
%ignore "\t"

COMMENT: "//"/[^\n]/*
%ignore COMMENT

?start: statement_list

// Used only for global scope
?statement_list: (statement | _NL)+

// Used for child lexical scopes, starts a new lexical scope
statement_block: statement_list

?statement: expression
		  | variable_declaration
		  | assignment
		  | if_chain
		  | match_stmt
		  | while_stmt
		  | struct_stmt
		  | enum_stmt
		  | print_stmt
		  | index_assignment
		  | function_definition
		  | return_stmt

variable_declaration: "let" IDENTIFIER "=" expression

assignment: IDENTIFIER "=" expression
index_assignment : variable "[" POS_DIGIT "]" "=" expression

print_stmt: "print" expression 

if_chain: if_stmt [else_if_ladder] [else_block]
if_stmt: "if" expression "{" statement_block "}"
else_if_ladder: else_if_stmt+
else_if_stmt: "elif" expression "{" statement_block "}"
?else_block: "else" "{" statement_block "}"

match_stmt: "match" expression "{" case_ladder "}"
case_ladder: (case_stmt | _NL)+
case_stmt: "case" match_pattern "{" statement_block "}"
?match_pattern: bool_literal | enum_literal

while_stmt: "while" expression "{" statement_block "}"

struct_stmt: "struct" IDENTIFIER "{" struct_members "}"
struct_members: (struct_member | _NL)+
struct_member: IDENTIFIER ":" IDENTIFIER

enum_stmt: "enum" IDENTIFIER "{" enum_members "}"
enum_members: (enum_member | _NL)+
enum_member: IDENTIFIER

function_definition: "fn" IDENTIFIER "(" [function_params] ")" "->" IDENTIFIER "{" statement_block "}"
function_params: function_param ("," function_param)*
function_param: IDENTIFIER ":" IDENTIFIER

return_stmt: "return" expression

?expression: equality

?!equality: logical ( "==" | "!=" ) equality
	 | logical

?!logical: comparison ( "&&" | "||" ) logical
	 | comparison

?!comparison: term ( ">" | "<" | "<=" | ">=") comparison
	 | term

?!term: factor ("+" | "-" ) term
	 | factor

?!factor: unary_op
	    | unary_op ("*" | "/" | "%") factor

?!unary_op: ("+" | "-" | "!") unary_op
		  | indexing

?indexing: atom "[" POS_DIGIT "]"
		|atom


POS_DIGIT: /[0-9]+/

?atom: bool_literal
	 | int_literal
	 | string_literal
	 | array_literal
	 | enum_literal
	 | "(" expression ")"
	 | variable
	


bool_literal: TRUE | FALSE
TRUE: "true"
FALSE: "false"

int_literal: INT

string_literal: STRING
STRING: "\"" /.*?/ "\""

enum_literal: IDENTIFIER "::" IDENTIFIER

variable: IDENTIFIER

array_literal: "[" array_elements "]"
array_elements: (array_element ("," array_element)*)?
array_element: expression
