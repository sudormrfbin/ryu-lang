%import common.INT
%import common.NEWLINE -> _NL
%import common.CNAME -> IDENTIFIER 

%ignore " "

?start: statement_list

// Used only for global scope
?statement_list: (statement | _NL)+

// Used for child lexical scopes, starts a new lexical scope
statement_block: statement_list

?statement: expression
		  | variable_declaration
		  | assignment
		  | if_chain
		  | match_stmt

variable_declaration: "let" IDENTIFIER "=" expression

assignment: IDENTIFIER "=" expression

if_chain: if_stmt [else_if_ladder] [else_block]
if_stmt: "if" expression "{" statement_block "}"
else_if_ladder: else_if_stmt+
else_if_stmt: "elif" expression "{" statement_block "}"
?else_block: "else" "{" statement_block "}"

match_stmt: "match" expression "{" case_ladder "}"
case_ladder: (case_stmt | _NL)+
case_stmt: "case" match_pattern "{" statement_block "}"
?match_pattern: bool_literal

?expression: equality

?!equality: logical ( "==" | "!=" ) equality
	 | logical

?!logical: comparison ( "&&" | "||" ) logical
	 | comparison

?!comparison: term ( ">" | "<" | "<=" | ">=") comparison
	 | term

?!term: factor ("+" | "-" ) term
	 | factor

?!factor: unary_op
	    | unary_op ("*" | "/" | "%") factor

?!unary_op: ("+" | "-" | "!") unary_op
		  | atom

?atom: bool_literal
	 | int_literal
	 | string_literal
	 | "(" start ")"
	 | variable

bool_literal: TRUE | FALSE
TRUE: "true"
FALSE: "false"

int_literal: INT

string_literal: STRING
STRING: "\"" /.*?/ "\""

variable: IDENTIFIER
